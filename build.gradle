/*
 * BSD 3-Clause License
 *
 * Copyright 2018  Sage Bionetworks. All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without modification,
 * are permitted provided that the following conditions are met:
 *
 * 1.  Redistributions of source code must retain the above copyright notice, this
 * list of conditions and the following disclaimer.
 *
 * 2.  Redistributions in binary form must reproduce the above copyright notice,
 * this list of conditions and the following disclaimer in the documentation and/or
 * other materials provided with the distribution.
 *
 * 3.  Neither the name of the copyright holder(s) nor the names of any contributors
 * may be used to endorse or promote products derived from this software without
 * specific prior written permission. No license is granted to the trademarks of
 * the copyright holders even if such marks are included in this software.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
 * SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
 * CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
 * OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.build_tools_version = '29.0.2'
    ext.kotlin_version = '1.3.50'
    ext.butterknife_version = '10.2.0'
    ext.guava_version = '27.0.1-android'
    ext.auto_value_version = '1.6.3'
    ext.auto_value_gson_version = '0.8.0'
    ext.dagger_version = '2.21'

    repositories {
        jcenter()
        google()
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'

        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"

        // deploy
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'

        // code quality
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.16'
        classpath 'com.chaitanyapramod.gradle:findbugs-android:1.1'

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.jakewharton:butterknife-gradle-plugin:$butterknife_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    version = '0.1.0'
    group = 'org.sagebionetworks.research'

    repositories {
        jcenter()
        google()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.plugins.hasPlugin('com.android.library')) {
            apply plugin: 'org.jetbrains.dokka-android'

            task sourcesJar(type: Jar) {
                from android.sourceSets.main.java.srcDirs
                classifier = 'sources'
            }

            dokka {
                outputFormat = 'html'
                outputDirectory = "$buildDir/javadoc"
            }

            task javadocJar(type: Jar, dependsOn: dokka) {
                classifier = 'javadoc'
                from dokka.outputDirectory
            }

            artifacts {
                // TODO: package javadoc for maven artifact publishing
                archives javadocJar
                archives sourcesJar
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
